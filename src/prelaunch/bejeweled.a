;license:MIT
;(c) 2020 by qkumba

!cpu 6502
!to "build/PRELAUNCH/BEJEWELED",plain
*=$106

    !source "src/prelaunch/common.a"

         +ENABLE_ACCEL
         lda   #<callback
         sta   $4534
         lda   #>callback
         sta   $4535
         jmp   $800       ; decompress

helper
         lda   $8696
         cmp   #$CB       ; write file
         bne   nohelp
         lda   $8AAB      ; write address (low)
         cmp   #$F3       ; erasing save
         beq   nohelp
         cmp   #$F5       ; writing "valid"
         beq   nocall     ; ignore it
         cmp   #$55       ; writing options
         beq   nohelp
         dec   $8AAB      ; write address

nohelp
         jmp   $D670      ; ProDOS_enter

nocall
         plp
         pla
         pla
         clc
         rts

callback
         jsr   $BE00
         lda   #<helper
         sta   $BF0B
         lda   #>helper
         sta   $BF0C
         +DISABLE_ACCEL
         rts

!if * > $1C0 {
  !error "code is too large, ends at ", *
}
