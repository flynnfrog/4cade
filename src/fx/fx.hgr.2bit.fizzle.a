;license:MIT
;(c) 2017-2020 by qkumba/4am/John Brooks

!cpu 6502
!to "build/FX/BIT2.FIZZLE",plain
*=$6000

addrs     = $6100         ; [256 bytes]
copymasks = $6200         ; [128 bytes]

         jsr   swapzp
         ldx   #0
         lda   #%00000011
--       ldy   #$20
         ora   #%10000000
-        sta   copymasks, x
         inx
         dey
         bne   -
         asl
         asl
         bne   --
         clc
         ldx   #0
--       ldy   #$20
         tya
-        sta   addrs, x
         adc   #1
         inx
         dey
         bne   -
         txa
         bne   --
         jsr   $0
swapzp   ldx   #(end-start-1)
-        lda   $0, x
         pha
         lda   start, x
         sta   $0, x
         pla
         sta   start, x
         dex
         bpl   -
         rts

start
!pseudopc 0 {
; in: X,Y=0
loop     txa
loop1    eor   #$60
         tax
loop2    lda   addrs, x
         sta   <dst+2
         eor   #$60
         sta   <src+2
         lda   (<dst+1), y
src      eor   $FD00, y
         and   copymasks, x
         eor   (<dst+1), y
dst      sta   $FD00, y
         txa
         lsr
         tax
         tya
         ror
         tay
         bcc   loop2
         bne   loop
         bit   $C000
         bmi   exit
         txa
         bne   loop1
exit     lda   $4000
         sta   $2000
         rts
}
end
