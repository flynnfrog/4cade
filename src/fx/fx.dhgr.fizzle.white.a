;license:MIT
;(c) 2017-2020 by qkumba/4am/John Brooks
;
!cpu 6502
!to "build/FX/DHGR.FIZZLEWH",plain
*=$6000

addrs=$C0                            ; [$40 bytes]

         ldx   #$1F                  ; build address lookup table
-        txa
         eor   #$20
         sta   addrs, x
         eor   #$80
         sta   addrs+$20, x
         dex
         bpl   -
         ldx   #(end-start)          ; copy LFSR code to zero page
-        lda   start-1, x
         sta   $0, x
         dex
         bne   -
         txa
         tay
         clv
         php
         bit   <knownrts             ; set V flag
         php
         pha
         pha
         rts

start
!pseudopc 1 {
; in: X,Y=0
loop     txa
loop1    eor   #$35                  ; LFSR form 0x3500 with period 16383
         tax
loop2    lda   addrs, x
         bpl   +
         and   #$7F
         sta   $C003
         sta   $C005
+        sta   <dst+2
op       ora   #$FF                  ; SMC
         sta   <src+2
src      ora   $FD00, y              ; SMC
dst      sta   $FD00, y
         sta   $C002
         sta   $C004
         txa
         lsr
         tax
         tya
         ror
         tay
         bcc   loop2
         bne   loop
         bit   $C000
         bmi   exit
         txa
         bne   loop1
         lda   #$49
         sta   op
         lda   #$60
         sta   op + 1
         lda   #$B9
         sta   <src
         plp
         bvs   loop
         !byte $a9
exit     plp
         bvs   exit
         lda   $4000                 ; last lousy byte (because LFSR never hits 0)
         sta   $2000
knownrts rts
}
end
