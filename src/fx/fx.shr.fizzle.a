;license:MIT
;(c) 2019-2020 by qkumba/4am

!cpu 6502
!to "build/FX/SHR.FIZZLE",plain
*=$a000

rnd1=$fe
rnd2=$ff

         ;init RNG

         ldy   #1
         sty   rnd1
         dey
         sty   rnd2

         ;copy SCB and palette

         lda   #$9d
         sta   $3d
         sty   $3c
         ldx   #$23                  ; copy SCB+palette+this code to auxmem
         sta   $c005
@copystuff
         lda   ($3c),y
         sta   ($3c),y
         iny
         bne   @copystuff
         inc   $3d
         dex
         bne   @copystuff

         ;force write offset 0 otherwise it will be missed

         lda   $2000
         sta   $2000
         sta   $c004

         ;iterate

@loop
         ldx   rnd1
@loop2
         stx   $26
         stx   $3c
         ldx   rnd2
         lsr   rnd2
         ror   rnd1
         bcc   +

         ;feedback polynomial forms #$4001 for period of 32767

         lda   rnd1
         eor   #1
         sta   rnd1
         lda   rnd2
         eor   #$40
         sta   rnd2

         ;don't rewrite SCB or palette
         ;it will make the fizzle take longer than obviously needed

+        cpx   #$7d
         bcs   +
         txa

         ;select address

         adc   #$20
         sta   $27
         sta   $3d

         ;copy pixel from other page to this page

         lda   ($3c),y
         sta   $c005
         sta   ($26),y
         sta   $c004

+
         ;exit conditions

         ldx   rnd2
         bne   @loop

         ldx   $c000
         bmi   @exit

         ldx   rnd1
         cpx   #1
         bne   @loop2

@exit    rts
